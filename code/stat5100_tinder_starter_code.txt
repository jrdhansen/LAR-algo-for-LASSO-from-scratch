/* This first line of code will need to be changed */
FILENAME REFFILE '/home/u41171697/data/project2/tinder.csv';

PROC IMPORT DATAFILE=REFFILE replace
	DBMS=CSV
	OUT=WORK.tinder;
	GETNAMES=YES;
RUN;

/* Check hard upper bound on genuine */
proc univariate data=tinder;
 var genuine;
 histogram genuine;
run;

proc sort data=tinder; by genuine;
data tinder; set tinder; ID = _n_;
proc sgplot data=tinder; 
  scatter x=ID y=genuine;
run;


/* Separate Into Training and Test Sets. 
Only Fit Models to the Training Set. The variable
"Selected" separates training (0) from test (1) */ 
proc surveyselect data=tinder seed=12345 out=tinder2
     rate=0.2 outall; /* Withold 20% for validation */
run;

data train; set tinder2;
if Selected = 0;
run;

data test; set tinder2;
if Selected = 1;
run;

/* Look at crude initial model */
proc reg data=train plots=(CooksD RStudentByLeverage DFFITS DFBETAS);
 model genuine = socprivconc instprivconc narcissism selfesteem loneliness
                 hookup friends partner travel selfvalidation entertainment
                 age impfitness impenergy impattractive / vif collin;
store regModel;
run;

/* Calculate MSPR */
proc plm restore=regModel;
 score data=test out=newTest predicted; 
 run;

data newTest; set newTest;
MSE = (Genuine - Predicted)**2;
run;

proc means data = newTest;
var MSE;
run;


